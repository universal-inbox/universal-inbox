set fallback

default:
    @just --choose

## Build recipes
build:
    trunk build --features trunk

build-release:
    trunk build --release --features trunk

build-assets: bundle-js build-tailwind bundle-fonts

build-tailwind:
    npx tailwindcss -i css/universal-inbox.css -o public/css/universal-inbox.min.css --minify

bundle-js:
    npx rspack build

bundle-fonts output-dir="public":
    mkdir -p {{ output-dir }}
    cp -a fonts {{ output-dir }}

## Dev recipes
check: install build-assets
    cargo clippy --tests -- -D warnings

install:
    npm install --dev

## Test recipes
test test-filter="" $RUST_LOG="info": build-assets
    cargo test {{test-filter}}

test-ci: install build-assets
    cargo test

## Run recipes
run: build-assets
    dx serve --verbose

run-tailwind:
    npx tailwindcss -i css/universal-inbox.css -o public/css/universal-inbox.min.css --minify --watch

run-bundle-js:
    npx rspack build --watch

run-trunk:
    trunk serve --features trunk
