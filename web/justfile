set fallback

default:
    @just --choose

## Build recipes
build: bundle-js build-tailwind
    trunk build

build-release:
    trunk build --release --features trunk

build-tailwind:
    npx tailwindcss -i css/universal-inbox.css -o public/css/universal-inbox.min.css --minify

bundle-js:
    npx rspack build

## Dev recipes
check: install bundle-js build-tailwind
    cargo clippy --tests -- -D warnings

install:
    npm install --dev

## Test recipes
test test-filter="" $RUST_LOG="info": bundle-js build-tailwind
    cargo test {{test-filter}}

test-ci: install bundle-js build-tailwind
    cargo test

## Run recipes
run: bundle-js build-tailwind
    dx serve --verbose

run-tailwind:
    npx tailwindcss -i css/universal-inbox.css -o public/css/universal-inbox.min.css --minify --watch

run-bundle-js:
    npx rspack build --watch

run-trunk:
    trunk serve --features trunk
