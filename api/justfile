set fallback

default:
    @just --choose

## Build recipes
build:
    cargo build

build-release:
    cargo build --release

## Dev recipes
check:
    cargo clippy --tests -- -D warnings

check-db:
    cargo sqlx prepare -- --bin universal-inbox-api
    cargo check --tests

migrate-db:
    sqlx database setup

## Test recipes
test test-filter="" $RUST_LOG="info":
    cargo nextest run --color always {{test-filter}}

test-ci:
    cargo nextest run --profile ci

test-coverage:
    cargo llvm-cov nextest --all-features --lcov --output-path lcov.info --profile ci

## Run recipes
run:
    watchexec --stop-timeout 10 --debounce 500 --exts toml,rs --restart --watch src cargo run --color always -- serve --embed-async-workers

run-api:
    watchexec --stop-timeout 10 --debounce 500 --exts toml,rs --restart --watch src cargo run --color always -- serve

run-workers:
    watchexec --stop-timeout 10 --debounce 500 --exts toml,rs --restart --watch src cargo run --color always -- start-workers

sync-tasks $RUST_LOG="info":
    cargo run -- sync-tasks

sync-notifications $RUST_LOG="info":
    cargo run -- sync-notifications

clear-cache:
    cargo run -- cache clear

generate-jwt-key-pair:
    cargo run -- generate-jwt-key-pair

generate-jwt-token user-email:
    cargo run -- generate-jwt-token {{user-email}}
